#!/usr/bin/perl -w

use strict;

use lib "/opt/Daemon";
use Chj_library;

our $servicename= xenv "SERVICE_NAME";
our $service_run_dir= xenv "SERVICE_RUN_DIR";
our $daemon= xenv "SERVICE_DAEMON";

our $pidfile="$service_run_dir/$servicename.pid";
our $lockfile="$service_run_dir/$servicename.lck";

use Fcntl ':flock';
use POSIX qw(setsid);

sub try_lock {
    # set up to get the close-on-exec flag cleared for the lockfh
    local $^F= 9999;
    # filehandle we will flock
    open my $lockfh, ">", $lockfile
      or die $!;
    if (flock $lockfh, LOCK_EX|LOCK_NB) {
	$lockfh
    } else {
	undef
    }
}


sub start {
    if (my $lockfh= try_lock) {
	if (my $pid = xfork) {
	    open my $pidfh, ">", $pidfile
	      or die $!;
	    print $pidfh "$pid\n"
	      or die $!;
	    xclose $pidfh;
	} else {
	    # child
	    setsid
	      or die $!;
	    # chdir "/";
	    # set up loggging and run daemon:
	    pipe my $r, my $w
	      or die $!;
	    if (xfork) {
		xclose $r;
		xdup2 $w, 1;
		xdup2 $w, 2;
		xclose $w;
		xexec $daemon;
	    } else {
		xclose $w;
		xdup2 $r, 0;
		xclose $r;
		xexec "logger", "-t", $servicename
	    }
	}
    } else {
	msg "already running."
    }
}

sub stop {
    if (my $lockfh= try_lock) {
	msg "not running.";
    } else {
	open my $pidfh, "<", $pidfile
	  or die $!;
	my $pid= <$pidfh>; chomp $pid;
	xclose $pidfh;
	# kill the process group:
	kill -2, $pid
	  or warn "could not kill $pid: $!";
	# wait until it ended:
	my $t=0;
	my $sleeptime= 0.1; # seconds
	my $msginterval = int(5 / $sleeptime); # every .. seconds
	my $endt= 30 / $sleeptime; # SIGKILL after .. seconds
	while (kill 0, -$pid) {
	    if ($t > $endt) {
		if (kill -9, $pid) {
		    msg "forcibly killed service with SIGKILL"
		} else {
		    warn "could not kill, stopped right before sending signal 9?: $!";
		}
		last;
	    }
	    $t++;
	    if (not $t % $msginterval) {
		msg ("still waiting for termination, will send SIGKILL in ".
		     (($endt - $t) * $sleeptime).
		     " seconds");
	    }
	    # sleep a bit:
	    select undef,undef,undef, $sleeptime;
	}
    }
}

sub usage {
    msg "usage: $0 start|stop|restart";
    exit 1;
}

my ($cmd)= @ARGV;
$cmd or usage;

my $cmds=
  +{
    stop=> \&stop,
    start=> \&start,
    restart=> sub {
	stop;
	start;
    },
   };

my $c= $$cmds{$cmd}
  or usage;
&$c;
